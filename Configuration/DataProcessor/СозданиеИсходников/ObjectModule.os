Перем Тексты;
Перем Каталог;

Функция ПолучитьСишноеПространствоИмен(Знач ПространствоИмен)
	
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "http://v8.1c.ru/", "");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, ".", "_");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "/", "::");
	ПространствоИмен = "v" + ПространствоИмен;
	
	Возврат ПространствоИмен;
	
КонецФункции

Функция ПолучитьИмяФайлаПространстваИмен(Знач ПространствоИмен)
	
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "http://v8.1c.ru/", "");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, ".", "_");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "-", "_");
	//ПространствоИмен = СтрЗаменить(ПространствоИмен, "/", "h/");
	ПространствоИмен = "/v" + ПространствоИмен + ".hxx";
	
	Возврат ПространствоИмен;
	
КонецФункции

Функция ПолучитьОбъявлениеСишногоПространстваИмен(Знач ПространствоИмен)
	
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "http://v8.1c.ru/", "");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, ".", "_");
	ПространствоИмен = "v" + ПространствоИмен;
	
	ПространствоИмен = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ПространствоИмен, "/");
	
	Объявление = "namespace v81c { ";
	Для Каждого Имя Из ПространствоИмен Цикл
		
		Объявление = Объявление + "namespace " + Имя + " { ";
		
	КонецЦикла;
	
	Возврат Объявление;
	
КонецФункции

Функция ПолучитьЗавершениеОбъявленияСишногоПространстваИмен(Знач ПространствоИмен)
	
	ПространствоИмен = СтрЗаменить(ПространствоИмен, "http://v8.1c.ru/", "");
	ПространствоИмен = СтрЗаменить(ПространствоИмен, ".", "_");
	
	ПространствоИмен = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ПространствоИмен, "/");
	
	Объявление = "} ";
	Для Каждого Имя Из ПространствоИмен Цикл
		
		Объявление = Объявление + "} ";
		
	КонецЦикла;
	
	Возврат Объявление;
	
КонецФункции

Процедура ДобавитьТекстВФайл(Знач ПространствоИмен, Знач ТекстОбъявления)
	
	Текст = Тексты.Получить(ПространствоИмен);
	Если Текст = Неопределено Тогда
		
		Текст = Новый ТекстовыйДокумент;
		Тексты.Вставить(ПространствоИмен, Текст);
		
		Текст.ДобавитьСтроку(ПолучитьОбъявлениеСишногоПространстваИмен(ПространствоИмен));
		
	КонецЕсли;
	
	Текст.ДобавитьСтроку(ТекстОбъявления);
	
КонецПроцедуры

Функция ВыполнитьСоздание() Экспорт
	
	Каталог = Новый Файл(КаталогВременныхФайлов() + "/xmr");
	УдалитьФайлы(Каталог.ПолноеИмя, "*");
	
	Каталог = Новый Файл(Каталог.ПолноеИмя + "/src");
	СоздатьКаталог(Каталог.ПолноеИмя);
	
	Тексты = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ Ссылка, Наименование, Владелец, Элементы
		|ИЗ Справочник.Типы
		|ГДЕ Владелец В ИЕРАРХИИ (&Пространство)
		|	И Тип = ЗНАЧЕНИЕ(Перечисление.ТипыТипов.Перечисление)
		|"
	;
	Запрос.УстановитьПараметр("Пространство", ПространствоИмен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Текст = "enum class " + Выборка.Наименование + " { " + Символы.ПС;
		Элементы = Выборка.Элементы.Выбрать();
		Пока Элементы.Следующий() Цикл 
			
			Текст = Текст + "    " + Элементы.Имя + ", " + Символы.ПС;
			
		КонецЦикла;
		
		Текст = Текст + "    __LAST" + Символы.ПС;
		Текст = Текст + "};" + Символы.ПС;
		
		ДобавитьТекстВФайл(Выборка.Владелец, Текст);
		
	КонецЦикла;
	
	ИмяЗип = Новый Файл(КаталогВременныхФайлов() + "/xmr/enums.zip");
	Zip = Новый ЗаписьZipФайла(ИмяЗип.ПолноеИмя);
	
	Для Каждого Текст Из Тексты Цикл
		
		ИмяФайла = Каталог.ПолноеИмя + ПолучитьИмяФайлаПространстваИмен(Текст.Ключ);
		Файл = Новый Файл(ИмяФайла);
		СоздатьКаталог(Файл.Путь);
	
		Текст.Значение.ДобавитьСтроку(ПолучитьЗавершениеОбъявленияСишногоПространстваИмен(Текст.Ключ));
		Текст.Значение.Записать(ИмяФайла, КодировкаТекста.UTF8);
		
	КонецЦикла;
	
	Zip.Добавить(Каталог.ПолноеИмя + "/*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
		
	Zip.Записать();
	
	Возврат ИмяЗип.ПолноеИмя;
	
КонецФункции
